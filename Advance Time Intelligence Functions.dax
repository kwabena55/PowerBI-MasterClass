% of Total Sales = 
VAR AllSales =
CALCULATE(
    [Customer Sales],
    ALL(
        'Sales by Store'
    )
)
VAR Ratio =
DIVIDE(
    [Customer Sales],
    AllSales
)
Return
Ratio

-------------------------------------------------
Customer Sales Last Month (PARALLELPERIOD) = 
CALCULATE(
    [Customer Sales],
    PARALLELPERIOD(
        'Calendar'[Transaction_Date],
        -1,
        MONTH
    )
)
-------------------------------------------------------
Customer Sales MoM % Change = 
DIVIDE(
    ([Customer Sales] - [Customer Sales Last Month (PARALLELPERIOD)]),
    [Customer Sales Last Month (PARALLELPERIOD)],
    BLANK()
)
------------------------------------------------------------------
Customer Sales YoY % Change = 
VAR LastYearsSales = 
CALCULATE(
    [Customer Sales],
    SAMEPERIODLASTYEAR(
        'Calendar'[Transaction_Date]
    )
)
VAR Ratio =
DIVIDE(
    ([Customer Sales] - LastYearsSales),
    LastYearsSales,
    "-"
)
Return
Ratio
------------------------------------------------------------------------------
Last QTD Sales (4-5-4) = 
VAR LastPeriod =
CALCULATE(
    [Customer Sales],
    FILTER(
        ALL(
            '4-5-4 Calendar'
        ),
        IF(
            SELECTEDVALUE('4-5-4 Calendar'[FiscalQuarter]) = 1,
            '4-5-4 Calendar'[FiscalQuarter] =4 && '4-5-4 Calendar'[FiscalYear] = SELECTEDVALUE('4-5-4 Calendar'[FiscalYear]) -1,
            '4-5-4 Calendar'[FiscalYear] = SELECTEDVALUE('4-5-4 Calendar'[FiscalYear]) &&
            '4-5-4 Calendar'[FiscalQuarter] = SELECTEDVALUE('4-5-4 Calendar'[FiscalQuarter]) -1
        )
    )
)
RETURN
LastPeriod
------------------------------------------------------------------
Last Quarter's Sales (PARALLELPERIOD) = 
CALCULATE(
    [Customer Sales],
    PARALLELPERIOD(
        'Calendar'[Transaction_Date],
        -1,
        QUARTER
    )
)

--------------------------------------------------------
Last Quarter's Sales (PREVIOUSQUARTER) = 
CALCULATE(
    [Customer Sales],
    PREVIOUSQUARTER(
        'Calendar'[Transaction_Date]
    )
)
----------------------------------------
Last Week's Sales 4-5-4 (DATEADD) = 
CALCULATE(
    [Customer Sales],
    DATEADD(
        '4-5-4 Calendar'[Date],
        -7,
        DAY
    )
)
------------------------------------------------------
Last Years Sales (SAMEPERIODLASTYEAR) = 
CALCULATE(
    [Customer Sales],
    SAMEPERIODLASTYEAR(
        'Calendar'[Transaction_Date]
    )
)

----------------------------------------
MTD Sales (4-5-4) = 
VAR MaxDate = MAX('4-5-4 Calendar'[Date])
VAR MaxPeriod = MAX('4-5-4 Calendar'[FiscalMonthYear])
VAR OutPut =
IF(
    HASONEVALUE(
        '4-5-4 Calendar'[FiscalMonthName]
    ),
    CALCULATE(
        [Customer Sales],
        '4-5-4 Calendar'[Date] <= MaxDate,
        '4-5-4 Calendar'[FiscalMonthYear] = MaxPeriod
    ),
    "-"
)
RETURN
OutPut
---------------------------------------------------------------
